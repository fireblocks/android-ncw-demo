plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.firebase.appdistribution'
    id 'com.google.gms.google-services'
}
apply from: '../versions.gradle'

android {
    namespace 'com.fireblocks.sdkdemo'
    compileSdk 34

    defaultConfig {
        applicationId "com.fireblocks.ncwdemo"
        minSdk 27
        targetSdk 34
        versionCode demoVersionCode
        versionName demoVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        archivesBaseName = "FireblocksSdkDemo-v$versionName"+"_"+"$versionCode"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    aaptOptions {
        additionalParameters =["--warn-manifest-validation"]
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            debuggable true
        }
    }
    // Specifies one flavor dimension.
    flavorDimensions += "server"
    flavorDimensions += "wallet"

    productFlavors {
        dev {
            dimension "server"
            applicationIdSuffix ".dev"
        }
        sandbox {
            dimension "server"
            applicationIdSuffix ""
            getIsDefault().set(true)
        }
        production {
            dimension "server"
            applicationIdSuffix ".prod"
        }
        ncw {
            dimension "wallet"
        }
        embeddedWallet {
            dimension "wallet"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api"
    }
    buildFeatures {
        viewBinding true
        buildConfig true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.3"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.aar"])
    implementation(name:'prefs-release', ext:'aar')
    implementation(name:'biometric-release', ext:'aar')

    implementation platform("org.jetbrains.kotlin:kotlin-bom:$kotlin_version")

    def nav_version = "2.7.4"
    implementation("androidx.navigation:navigation-compose:$nav_version")
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    def lifecycle_version = "2.6.2"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.activity:activity-compose:1.8.0"

    // Compose
    def composeBom = platform('androidx.compose:compose-bom:2023.10.01')
    implementation composeBom
    androidTestImplementation composeBom
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.material3:material3'
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.material:material"
    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'

    implementation 'androidx.core:core-ktx'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.2"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    def artifactIdPrefix = useProductionFlavor.toBoolean() ? "" : "-dev"
    println("artifactIdPrefix: " + artifactIdPrefix + ", useEmbeddedWallet: " + useEmbeddedWallet + ", useProductionFlavor: " + useProductionFlavor + ", fireblocksBomVersion: " + fireblocksBomVersion)
    if (useBom.toBoolean() == true) {
        // Use BOM for Fireblocks SDKs
        println("use com.fireblocks.sdk:bom$artifactIdPrefix:$fireblocksBomVersion")
        implementation(platform("com.fireblocks.sdk:bom$artifactIdPrefix:$fireblocksBomVersion"))
    }
    implementation "com.fireblocks.sdk:ncw$artifactIdPrefix"
    if (useEmbeddedWallet.toBoolean()) {
        // define dependencies without versions
        embeddedWalletImplementation "com.fireblocks.sdk:ew$artifactIdPrefix"
    }

//    productionImplementation "com.fireblocks.sdk:ncw:$sdkVersionName"
//    sandboxImplementation "com.fireblocks.sdk:ncw:$sdkVersionName"
//
//    println("useProductionPublicRepo: " + useProductionPublicRepo)
//    if (useProductionPublicRepo.toBoolean() == false) {
//        println("get sdk from dev private repo")
//        devImplementation "com.fireblocks.sdk:ncw-dev:$sdkVersionName"
//    }

    implementation 'com.jakewharton.timber:timber:5.0.1'
    //network
    implementation('com.squareup.retrofit2:retrofit:2.9.0') {
        exclude group: 'com.google.code.gson'
    }
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'

    //Coil for compose
    api 'io.coil-kt:coil-compose:2.4.0'

    implementation "com.google.accompanist:accompanist-systemuicontroller:0.31.5-beta"

    // Import the BoM for the Firebase platform
    implementation(platform("com.google.firebase:firebase-bom:32.3.1"))
    implementation 'com.google.firebase:firebase-analytics-ktx'
    
    // Add the dependency for the Firebase Authentication library
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation("com.google.firebase:firebase-auth-ktx")

    // Also add the dependency for the Google Play services library and specify its version
    implementation("com.google.android.gms:play-services-auth:20.7.0")

    // Zxing
    implementation "com.google.zxing:core:3.5.1"
    implementation('com.journeyapps:zxing-android-embedded:4.3.0')

    // Guava
    implementation "com.google.guava:guava:24.1-jre"
    // Guava fix
    implementation "com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava"

    //Drive
    implementation('com.google.api-client:google-api-client-android:1.23.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude module: 'guava-jdk5'
    }
    implementation('com.google.apis:google-api-services-drive:v3-rev136-1.25.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude module: 'guava-jdk5'
    }
    // get WIF for Bitcoin asset
    implementation("org.bitcoinj:bitcoinj-core:0.16.2")
}